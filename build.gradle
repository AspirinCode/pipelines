/* Copyright 2017 Informatics Matters Ltd.
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
* http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/


apply plugin: 'com.bmuschko.docker-remote-api'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.5'
    }
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

version = 1.0

task cleanDist(type: Delete) {
    group = 'Distribution'
    delete 'build/dist'
}


task dist(type: Copy) {
    group = 'Distribution'
    dependsOn cleanDist

    destinationDir = file('build/dist')

    from('src/python') {
        includes = ['**/*.py','**/*.nf','**/*.dsd','README.rst']
        into 'pipelines'
    }

    from('src/nextflow') {
        includes = ['**/*.py','**/*.nf','**/*.dsd','README.rst']
        into 'nextflow'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    jarFile = 'wrapper/wrapper.jar'
}

/** Build a Docker image
 * run the generated image with something like this:
 *
 * docker run -it --rm informaticsmatters/pipelines bash
 */
docker {
    certPath = null
}

task buildDockerFile(type: Dockerfile, dependsOn: dist) {

    destFile = project.file('build/Dockerfile')
    from "informaticsmatters/rdkit_java:Release_2017_03_1"
    maintainer 'Tim Dudgeon <tdudgeon@informaticsmatters.com>'

    addFile('dist/pipelines', "/pipelines")
    addFile('dist/nextflow', "/nextflow")
}

task buildDockerImage(type: DockerBuildImage, dependsOn: buildDockerFile) {
    inputDir = buildDockerFile.destFile.parentFile
    tag = "informaticsmatters/pipelines"
}